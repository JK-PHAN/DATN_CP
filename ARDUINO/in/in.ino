#include <SPI.h>
#include <LoRa.h>
#include "Adafruit_VEML6070.h"

// ChÃ¢n LoRa
#define ss 10
#define rst 9
#define dio0 2

// ChÃ¢n cáº£m biáº¿n
#define lm335Pin A0
#define inputPin 3  // NE555 táº§n sá»‘ Ä‘áº§u ra

// ChÃ¢n Ä‘iá»u khiá»ƒn thiáº¿t bá»‹
#define QUAT_PIN 5
#define REM_PIN 4

// UV sensor
Adafruit_VEML6070 uv = Adafruit_VEML6070();

// Biáº¿n toÃ n cá»¥c
volatile unsigned long pulseCount = 0;
const unsigned long duration = 1000;
const unsigned long sendInterval = 30000;
const unsigned long loopInterval = 15000;
unsigned long previousSendTime = 0;
unsigned long previousLoopTime = 0;
#define NUM_SAMPLES 10

// Tráº¡ng thÃ¡i rÃ¨m
bool remState = false; // Biáº¿n lÆ°u tráº¡ng thÃ¡i rÃ¨m, ban Ä‘áº§u lÃ  chÆ°a kÃ©o
// Tráº¡ng thÃ¡i quáº¡t
bool quatState = false; // Biáº¿n lÆ°u tráº¡ng thÃ¡i quáº¡t, ban Ä‘áº§u lÃ  chÆ°a báº­t

void setup() {
  Serial.begin(9600);
  while (!Serial);
  // Khá»Ÿi Ä‘á»™ng LoRa
  Serial.println("ğŸš€ LoRa Sender Starting...");

  LoRa.setPins(ss, rst, dio0);
  while (!LoRa.begin(433E6)) {
    Serial.println("âŒ LoRa init failed. Äang thá»­ láº¡i...");
    delay(1000);
  }
  LoRa.setSyncWord(0xF3);
  Serial.println("âœ… LoRa khá»Ÿi táº¡o thÃ nh cÃ´ng!");
  // Khá»Ÿi Ä‘á»™ng cáº£m biáº¿n UV, chÃ¢n quáº¡t vÃ  rÃ¨m
  pinMode(inputPin, INPUT);
  pinMode(QUAT_PIN, OUTPUT);
  digitalWrite(QUAT_PIN, LOW); // Äáº£m báº£o quáº¡t táº¯t lÃºc báº¯t Ä‘áº§u

  pinMode(REM_PIN, OUTPUT);
  digitalWrite(REM_PIN, HIGH); // Äáº£m báº£o rÃ¨m táº¯t lÃºc Ä‘áº§u Ä‘ang má»Ÿ
  
  attachInterrupt(digitalPinToInterrupt(inputPin), countPulse, RISING);

  uv.begin(VEML6070_1_T);
}

void loop() {
  unsigned long currentMillis = millis(); // Láº¥y thá»i gian hiá»‡n táº¡i
  // Biáº¿n lÆ°u trá»¯ dá»¯ liá»‡u cáº£m biáº¿n
  static float temperatureC = 0.0;
  static float humidity = 0.0;
  static float uvLevel = 0.0;

  // Cá»© 10 giÃ¢y Ä‘o láº¡i sensor
  if (currentMillis - previousLoopTime >= loopInterval) { // Kiá»ƒm tra thá»i gian Ä‘Ã£ trÃ´i qua
    previousLoopTime = currentMillis;

    bool successTemp = readTemperature(temperatureC);
    bool successHum = readHumidity(humidity);
    bool successUV = readUV(uvLevel);

    if (!successTemp || !successHum || !successUV) {
      Serial.println("âš ï¸ Má»™t hoáº·c nhiá»u cáº£m biáº¿n Ä‘á»c lá»—i, bá» qua chu ká»³ nÃ y.");
      return;
    }
    // In dá»¯ liá»‡u trung bÃ¬nh
    Serial.println("--------------------------------");
    Serial.println("=== Dá»¯ liá»‡u mÃ´i trÆ°á»ng (trung bÃ¬nh) ===");
    Serial.print("ğŸŒ¡ï¸ Nhiá»‡t Ä‘á»™: "); Serial.print(temperatureC, 1); Serial.println(" Â°C");
    Serial.print("ğŸ’§ Äá»™ áº©m: "); Serial.print(humidity, 1); Serial.println(" %");
    Serial.print("ğŸŒ UV: "); Serial.println(uvLevel);

    controlDevices(temperatureC, humidity, uvLevel);
  }

  // Cá»© 30 giÃ¢y gá»­i dá»¯ liá»‡u qua LoRa
  if (currentMillis - previousSendTime >= sendInterval) {
    previousSendTime = currentMillis;
    sendLoRaData(temperatureC, humidity, uvLevel);
  }
}
// HÃ m Ä‘á»c nhiá»‡t Ä‘á»™ tá»« cáº£m biáº¿n LM335
bool readTemperature(float &avgTemp) {
  float sum = 0;
  int success = 0;
  for (int i = 0; i < NUM_SAMPLES; i++) {
    int val = analogRead(lm335Pin); // Äá»c giÃ¡ trá»‹ tá»« cáº£m biáº¿n LM335
    if (val > 0 && val < 1024) {
      float voltage = val * (5.0 / 1023.0);
      sum += voltage * 20.0;  // Vout=V/5*100: nhÃ¢n chÃ©o chia ngÆ°á»£c ta Ä‘Æ°á»£c nhiá»‡t Ä‘á»™ C
      success++;
    }
    delay(10);
  }

  if (success == 0) {
    avgTemp = 0;
    return false;  // KhÃ´ng Ä‘á»c Ä‘Æ°á»£c máº«u há»£p lá»‡
  }
  // TÃ­nh giÃ¡ trá»‹ trung bÃ¬nh
  avgTemp = sum / success;
  return true;  // Äá»c thÃ nh cÃ´ng
}

// HÃ m Ä‘á»c Ä‘á»™ áº©m tá»« cáº£m biáº¿n NE555
bool readHumidity(float &avgHum) {
  float sum = 0;
  int success = 0;
  for (int i = 0; i < NUM_SAMPLES; i++) {
    pulseCount = 0;
    unsigned long start = millis();
    while (millis() - start < duration);
    float freq = (pulseCount / 2.0) * (1000.0 / duration);
    if (freq > 0) {
      sum += convertFrequencyToHumidity(freq);
      success++;
    }
    delay(10);
  }

  if (success == 0) {
    avgHum = 0;
    return false;  // KhÃ´ng Ä‘á»c Ä‘Æ°á»£c máº«u há»£p lá»‡
  }
  avgHum = sum / success;
  return true;
}

// HÃ m Ä‘á»c UV tá»« cáº£m biáº¿n VEML6070
bool readUV(float &avgUV) {
  float sum = 0;
  int success = 0;
  for (int i = 0; i < NUM_SAMPLES; i++) {
    float uvVal = uv.readUV() * 0.125; // Chuyá»ƒn Ä‘á»•i giÃ¡ trá»‹ UV
    if (uvVal >= 0) {
      sum += uvVal;
      success++;
    }
    delay(10);
  }

  if (success == 0) {
    avgUV = 0;  
    return false;  // KhÃ´ng Ä‘á»c Ä‘Æ°á»£c máº«u há»£p lá»‡
  } 
  avgUV = round((sum / success) * 100) / 100.0;
  return true;
  
}

// HÃ m gá»­i dá»¯ liá»‡u qua LoRa
void sendLoRaData(float temp, float hum, float uv) {
  String data = "ID2,T:" + String(temp, 1) +
                ",H:" + String(hum, 1) +
                ",UV:" + String(uv);

  LoRa.beginPacket();
  LoRa.print(data);
  LoRa.endPacket();

  Serial.println("ğŸ“¡ ÄÃ£ gá»­i qua LoRa: " + data);
  Serial.println("--------------------------------");
}

// HÃ m Ä‘iá»u khiá»ƒn thiáº¿t bá»‹ dá»±a trÃªn dá»¯ liá»‡u cáº£m biáº¿n
void controlDevices(float temp, float hum, float uv) {
  // Quáº¡t
  if (!quatState && (temp > 27 || hum > 70)) {
    digitalWrite(QUAT_PIN, HIGH);
    quatState = true;
    Serial.println("Báº¬T QUáº T");
  } else if (quatState && (temp < 25 || hum < 65)) {
    digitalWrite(QUAT_PIN, LOW);
    quatState = false;
    Serial.println("Táº®T QUáº T");
  }

  // RÃ¨m
  if (!remState && uv > 4.5) {
    digitalWrite(REM_PIN, LOW);
    remState = true;
    Serial.println("Báº¬T KÃ‰O RÃˆM"); // keo ra
  } else if (remState && uv < 3.0) {
    digitalWrite(REM_PIN, HIGH);
    remState = false;
    Serial.println("Táº®T KÃ‰O RÃˆM"); //keo vao
  }
}

// HÃ m Ä‘áº¿m xung tá»« cáº£m biáº¿n NE555
void countPulse() {
  pulseCount++;
}
// HÃ m chuyá»ƒn Ä‘á»•i táº§n sá»‘ sang Ä‘á»™ áº©m
float convertFrequencyToHumidity(float frequency) {
  const int RH[] =     {10, 15, 20, 25, 30, 35, 40, 45, 50, 55, 60, 65, 70, 75, 80, 85, 90, 95};
  const float Fout[] = {7155,7080,7010,6945,6880,6820,6760,6705,6650,6600,6550,6500,6450,6400,6355,6305,6260,6210};
  int size = sizeof(Fout) / sizeof(Fout[0]);

  int i;
  for (i = 0; i < size - 1; i++) {
    if (frequency > Fout[i]) break;
  }

  if (i == 0) return RH[0];
  if (i >= size) return RH[size - 1];

  float freq1 = Fout[i], freq2 = Fout[i - 1];
  float rh1 = RH[i], rh2 = RH[i - 1];
  return rh1 + (frequency - freq1) * (rh2 - rh1) / (freq2 - freq1);
}
